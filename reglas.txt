programa:
	declara_objetos variables_globales declara_funciones implementa_funciones EJECUTARPROGRAMA ALLAVE variables_locales bloque CLLAVE


variables_globales:
	VARIABLES_GLOBALES bloque_variables


bloque_variables:
	ACORCHETE bloque_variables_rep CCORCHETE


bloque_variables_rep:
	/* empty */
	| 
	declara_variables bloque_variables_rep


declara_variables:
	ENTERO IDENTIFICADOR PUNTOYCOMA
	|
	DECIMAL IDENTIFICADOR PUNTOYCOMA
	|
	TEXTO IDENTIFICADOR PUNTOYCOMA
	|
	BOOLEANO IDENTIFICADOR PUNTOYCOMA
	|
	MATRIZDECIMAL IDENTIFICADOR dimensiones PUNTOYCOMA
	|
	MATRIZENTERA IDENTIFICADOR dimensiones PUNTOYCOMA
	|
	CREAROBJETO IDENTIFICADOR ES IDENTIFICADOR PUNTOYCOMA


dimensiones:
	ACORCHETE CTEENTERA CCORCHETE dimensiones_rep


dimensiones_rep:
	/*empty*/
	| 
	ACORCHETE CTEENTERA CCORCHETE


dimensiones2:
	/*Empty*/
	|
	ACORCHETE serexpresion2 CCORCHETE


serexpresion: 
	expresion expresion_and_or


expresion_and_or:
	/*empty*/
	| 
	AMPERSAND serexpresion 
	| 
	BARRA serexpresion 


expresion:
	exp expresion_condicional


expresion_condicional:
	/*empty*/
	| 
	op_booleanos exp


op_booleanos:
	COMPARA 
	| 
	NEGACION 
	| 
	MAYORQUE 
	| 
	MENORQUE 
	| 
	MAYORIGUAL 
	| 
	MENORIGUAL


exp:
	termino exp_suma_resta


exp_suma_resta:
	/*Empty*/
	| 
	MAS exp 
	| 
	MENOS exp


termino:
	factor termino_multi_divide


termino_multi_divide:
	/*Empty*/
	|
	POR termino 
	| 
	ENTRE termino
	

factor:
	APARENTESIS serexpresion CPARENTESIS
	| 
	var_cte
	

var_cte:
	CTEENTERA
	|
	CTEDECIMAL
	|
	CTETEXTO
	| 
	CTEBOOLEANO
	| 
	identificadorOLlamadaAFuncion	


identificadorOLlamadaAFuncion:
	IDENTIFICADOR opcionalFuncion


opcionalFuncion:
	/*Empty*/
	| 
	llama_funcion_opcional APARENTESIS parametros_funcion CPARENTESIS
	| 
	ACORCHETE serexpresion2 CCORCHETE dimensiones2


opcionalFuncion2:
	llama_funcion_opcional APARENTESIS parametros_funcion CPARENTESIS


parametros_funcion:
	/*Empty*/
	| 
	serexpresion2 rep_parametros
	

rep_parametros:
	/*Empty*/
	|
	COMA parametros_funcion
	

declara_objetos:
	DECLARA_OBJETOS ACORCHETE declara_objetos_rep CCORCHETE


declara_objetos_rep:
	/*Empty*/
	|
	objeto declara_objetos_rep
	

objeto:
	CLASE IDENTIFICADOR objeto_herencia ALLAVE atributos_globales declara_funciones implementa_funciones CLLAVE

objeto_herencia:
	/*Empty*/
	| 
	HIJODE IDENTIFICADOR
	

atributos_globales:
	ATRIBUTOS_GLOBALES bloque_variables


declara_funciones:
	DECLARA_FUNCIONES ACORCHETE declara_funciones_rep CCORCHETE
	

declara_funciones_rep:
	/*Empty*/
	|
	declaracion_prototipos declara_funciones_rep
	

declaracion_prototipos:
	IDENTIFICADOR APARENTESIS parametros CPARENTESIS REGRESA declaracion_prototipos_regresa PUNTOYCOMA
	

declaracion_prototipos_regresa:
	tipo
	|
	NADA
	

parametros:
	/*Empty*/
	| 
	parametros_rep
	

parametros_rep:
	IDENTIFICADOR DOSPUNTOS tipo parametros_rep1
	

parametros_rep1:
	/*Empty*/
	|
	COMA parametros_rep
	

tipo:	
	ENTERO
	| 
	DECIMAL 
	| 
	BOOLEANO 
	| 
	TEXTO 
	| 
	MATRIZENTERA 
	| 
	MATRIZDECIMAL


implementa_funciones:
	IMPLEMENTA_FUNCIONES ACORCHETE implementa_funciones_rep CCORCHETE


implementa_funciones_rep:
	/*Empty*/
	|
	funciones implementa_funciones_rep
	

funciones:
	IDENTIFICADOR APARENTESIS parametros CPARENTESIS ALLAVE variables_locales bloque CLLAVE


variables_locales:
	VARIABLES_LOCALES bloque_variables
	

bloque:
	/*Empty*/
	|
	estatuto bloque
	

estatuto:
	decideEstatuto PUNTOYCOMA
	|
	escritura PUNTOYCOMA
	|
	regresa PUNTOYCOMA
	|
	lectura PUNTOYCOMA
	|
	condicional
	|
	ciclo
	

decideEstatuto:
	IDENTIFICADOR estatutoOAsignacion


estatutoOAsignacion:
	asignacion 
	|
	opcionalFuncion2
	

llama_funcion_opcional:
	/*Empty*/
	| 
	FLECHA IDENTIFICADOR
	

asignacion:
	ACORCHETE serexpresion2 CCORCHETE dimensiones2 IGUAL serexpresion
	| 
	IGUAL serexpresion


lectura:
	LEERDELTECLADO APARENTESIS IDENTIFICADOR lectura_matriz CPARENTESIS

lectura_matriz:
	/*Empty*/
	|
	ACORCHETE serexpresion2 CCORCHETE dimensiones2
	
	
escritura:
	DESPLIEGA APARENTESIS escritura_valores CPARENTESIS 
	

escritura_valores:
	serexpresion escritura_valores1
	

escritura_valores1:
	/*Empty*/
	| 
	CONCATENA escritura_valores
	

regresa:
	REGRESA serexpresion
	

condicional:
	SI APARENTESIS serexpresion CPARENTESIS ALLAVE bloque CLLAVE condicional_if


condicional_if:
	/*Empty*/
	| 
	SINO ALLAVE bloque CLLAVE
	

ciclo:
	MIENTRAS APARENTESIS serexpresion CPARENTESIS ALLAVE bloque CLLAVE 
	

serexpresion2: 
	expresion2 expresion_and_or2
	

expresion_and_or2:
	/*empty*/
	|
	AMPERSAND serexpresion2 
	| 
	BARRA serexpresion2 
	

expresion2:
	exp2 expresion_condicional2
	

expresion_condicional2:
	/*empty*/
	|
	op_booleanos2 exp2
	

op_booleanos2:
	COMPARA 
	|
	NEGACION 
	|
	MAYORQUE
	|
	MENORQUE 
	|
	MAYORIGUAL
	|
	MENORIGUAL
	

exp2:
	termino2 exp_suma_resta2
	

exp_suma_resta2:
	/*Empty*/
	|
	MAS exp2
	| 
	MENOS exp2
	

termino2:
	factor2 termino_multi_divide2
	

termino_multi_divide2:
	/*Empty*/
	|
	POR termino2 
	| 
	ENTRE termino2


factor2:
	APARENTESIS serexpresion2 CPARENTESIS
	| 
	var_cte2
	

var_cte2:
	CTEENTERA
	| 
	CTEDECIMAL
	| 
	CTETEXTO
	| 
	CTEBOOLEANO
	| 
	IDENTIFICADOR